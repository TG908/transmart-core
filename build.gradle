buildscript {
    ext {
        assetPipelineVersion = '2.11.6'
        grailsVersion = '3.2.3'
        guavaVersion = '19.0'
        jansiVersion = '1.14'
        protobufVersion = '3.1.0'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'https://repo.grails.org/grails/core' }
        maven { url 'https://dl.bintray.com/grails/plugins' }
        maven { url 'https://repo.transmartfoundation.org/content/repositories/public' }
        maven { url 'https://repo.thehyve.nl/content/repositories/public' }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.google.guava:guava:$guavaVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:$assetPipelineVersion"
        classpath 'org.grails.plugins:hibernate5:6.0.4'
        classpath "com.google.protobuf:protobuf-java:${protobufVersion}"
        classpath "com.google.protobuf:protobuf-java-util:${protobufVersion}"
        classpath 'org.grails.plugins:views-gradle:1.0.12'
        classpath 'net.linguica.gradle:maven-settings-plugin:0.5'
        classpath "org.fusesource.jansi:jansi:${jansiVersion}"
        classpath 'org.owasp:dependency-check-gradle:3.2.1'
    }
}

// Workaround suggested in https://github.com/gradle/gradle/issues/778
org.fusesource.jansi.AnsiConsole.out.println('[init jansi library conflict workaround]')

apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'org.owasp.dependencycheck'

ext {
    transmartVersion = '17.1-HYVE-5.8-RC1'

    assetPipelineVersion = '2.11.6'
    gradleWrapperVersion = '3.5.1'
    grailsVersion = '3.2.3'
    guavaVersion = '19.0'
    jacksonVersion = '2.9.5'
    modelMapperVersion = '1.1.3'
    ojdb7Version = '12.1.0.1'
    openCsvVersion = '4.2'
    postgresqlVersion = '42.1.4'
    protobufVersion = '3.1.0'
    quartzVersion = '2.0.13'
    springSecurityOauth2PluginVersion = '3.0.0-RC2'
    springSecurityPluginVersion = '3.1.1'
    springSecurityVersion = '3.2.3.RELEASE'
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

version = transmartVersion
group = 'org.transmartproject'

allprojects {
    ext {
        assetPipelineVersion = assetPipelineVersion
        guavaVersion = guavaVersion
        jacksonVersion = jacksonVersion
        modelMapperVersion = modelMapperVersion
        ojdb7Version = ojdb7Version
        openCsvVersion = openCsvVersion
        postgresqlVersion = postgresqlVersion
        protobufVersion = protobufVersion
        quartzVersion = quartzVersion
        springSecurityOauth2PluginVersion = springSecurityOauth2PluginVersion
        springSecurityPluginVersion = springSecurityPluginVersion
        springSecurityVersion = springSecurityVersion
    }
    repositories {
        repositories {
            mavenLocal()
            mavenCentral()
            maven { url 'https://repo.grails.org/grails/core' }
            maven { url 'https://repo.transmartfoundation.org/content/repositories/public/' }
            maven { url 'https://repo.thehyve.nl/content/groups/public/' }
            maven { url 'https://dl.bintray.com/bluesliverx/grails-plugins' }
            //maven { url 'https://jcenter.bintray.com' }
        }
    }
}

subprojects { project ->
    version = transmartVersion
    group = 'org.transmartproject'

    apply plugin: 'eclipse'
    apply plugin: 'idea'

    if (project.name =~ /^(biomart-domain|folder-management-plugin|mock-ontology-server|Rmodules|search-domain|transmart-core-db-tests|transmart-core-db|transmart-gwas-plugin|transmart-legacy-db|transmart-rest-api|transmartApp|transmart-server|transmart-oauth|transmart-solr-indexing|transmart-notifications|transmart-api-server)$/) {
        if (project.name =~ /^(transmartApp|mock-ontology-server|transmart-server|transmart-solr-indexing|transmart-api-server)$/) {
            apply plugin: 'war'
            apply plugin: 'org.grails.grails-web'
        } else {
            apply plugin: 'org.grails.grails-plugin'
            bootRepackage.enabled = false
        }
        apply plugin: 'org.grails.grails-gsp'
        apply plugin: 'asset-pipeline'

        // See https://docs.gradle.org/3.2/release-notes.html#usage-of-jansi-library-embedded-with-java-annotation-processor
        tasks.withType(JavaCompile) {
            options.fork = true
        }

        dependencyManagement {
            imports {
                mavenBom "org.grails:grails-bom:$grailsVersion"
            }
            applyMavenExclusions false
        }
    }
    if (project.name =~ /^(transmart-server|transmart-core-api|transmart-data|transmart-batch|transmart-copy|transmart-api-server)$/) {
        apply plugin: 'net.linguica.maven-settings'
        apply plugin: 'maven-publish'
        publishing {
            repositories {
                maven {
                    name "nl.thehyve.nexus"
                    url "https://repo.thehyve.nl/content/repositories/releases/"
                }
            }
        }
    }
}

